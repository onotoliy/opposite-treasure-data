buildscript {
    ext {
        kotlin_version = '1.3.50'
        serializer_version = '0.12.0'
    }
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlinx/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.50'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.github.onotoliy.opposite'
version "1.0.11"

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.multiplatform'

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/kotlin/kotlinx/"
    }
    maven {
        url  "https://dl.bintray.com/a-k-pohresniy/onotoliy"
    }
}

kotlin {
    jvm(){
        mavenPublication {
            version = "$version"
            artifactId = "treasure-data-jvm"
        }
    }
    js(){
        compilations.all {
            kotlinOptions.moduleKind = "umd"
        }
        mavenPublication {
            version = "$version"
            artifactId = "treasure-data-js"
        }
    }
    metadata() {
        mavenPublication {
            version = "$version"
            artifactId = "treasure-data-metadata"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializer_version"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializer_version"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serializer_version"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            url = 'https://dl.bintray.com/a-k-pohresniy/onotoliy/'
        }
    }

    publications {
        jvm() {
            artifactId = "treasure-data-jvm"
        }
        js() {
            artifactId = "treasure-data-js"
        }
        metadata() {
            artifactId = "treasure-data-metadata"
        }
    }
}

bintray {
    user = property('onotoliy.bintray.user')
    key = property('onotoliy.bintray.key')
    publications = ['jvm', 'js', 'metadata']
    publish = true
    pkg {
        repo = 'onotoliy'
        name = 'treasure-data'
        userOrg = 'a-k-pohresniy'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/onotoliy/opposite-treasure-data.git'
    }
    version {
        name = project.version
        released = new Date()
        vcsTag = project.version

        gpg {
            sign = true
            passphrase = property('onotoliy.bintray.passphrase')
        }
    }
}